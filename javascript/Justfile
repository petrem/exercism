# By default, list available recipes
_default:
    @just --list

_dontknow:
    @echo "Don't know how to do that yet"

# Clean up
clean:
    #!/usr/bin/env bash
    set -euo pipefail
    echo 'Cleaning up {{file_name(invocation_directory())}}...'
    cd {{invocation_directory()}}
    rm -f *~
    rm -rf node_modules

# Run linters, static analysers and so on
check +targets:
    #!/usr/bin/env bash
    set -euo pipefail
    echo 'Checking {{file_name(invocation_directory())}}...'
    cd {{invocation_directory()}}
    npm run lint {{targets}}
    

# Auto-format code file
style *targets:
    @echo 'Styling {{invocation_directory()}}/{{targets}}...'
    @just _dontknow

# Run tests
test:
    #!/usr/bin/env bash
    set -euo pipefail
    echo 'Testing {{file_name(invocation_directory())}}...'
    cd {{invocation_directory()}}
    npm run test

# Run style, lint, tests, submit to exercism, git commit
submit +targets:
    #!/usr/bin/env bash
    set -euo pipefail
    echo 'Submitting {{invocation_directory()}}/{{targets}}...'
    cd {{invocation_directory()}}
    just style 
    just check {{targets}}
    just test
    exercism submit {{targets}}
    git add .
    git commit -m 'exercism:javascript:{{file_name(invocation_directory())}}'
